comentario = { "(" ~ (!")" ~ ANY)* ~ ")" }

WHITESPACE = _{ " " | "\n" | comentario }
coma = _{ "," }
y_punto = _{ "y" }
puntuacion = _{ "." | coma | y_punto }
puntuacion_estricta = _{ "." | "," }

predicado = _{ ":" }

programa = { "hola" ~ puntuacion ~ lista_sentencias ~ "adios" ~ puntuacion_estricta? }

lista_sentencias = { sentencia* }

peticion = _{
    "por favor" ~ puntuacion*
        ~ lista_sentencias
    ~ "gracias" ~ puntuacion_estricta }

sentencia = _{ sentencia_bloque | (sentencia_puntuada ~ puntuacion) }

sentencia_puntuada = _{ baja | muestra | di | "" }
sentencia_bloque = _{ peticion | repite_veces }

expr_op = { "mas" | "menos" }
expresion = { producto ~ ( expr_op ~ producto)* }

prod_op = { "por" | "entre" }
producto = { literal_numerico ~ (prod_op ~ literal_numerico)* }

literal_numerico = _{ numero | verdad }
literal = _{ numero | verdad | texto }

numero = @{ (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) | "0" }

verdad = { "cierto" | "falso" }

texto_contenido = @{ (!"\"" ~ ANY)* }

texto = @{ "\"" ~ texto_contenido ~ "\"" }

di = { "di" ~ texto }
muestra = { "muestra" ~ expresion }
baja = { "baja" }

repite_veces = { "repite" ~ expresion ~ "veces" ~ predicado ~ sentencia }